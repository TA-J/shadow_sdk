// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    {// 读取versions.properties到ext中，供项目中直接用变量引用版本号
        def versions_properties_path = 'buildScripts/gradle/versions.properties'
        def versions = new Properties()
        versions.load(file(versions_properties_path).newReader())
        versions.forEach { key, value ->
            ext.set(key, value)
        }
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.tencent.shadow.coding:aar-to-jar-plugin'
        classpath 'com.tencent.shadow.coding:common-jar-settings'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
allprojects {
    repositories {
        if (!System.getenv().containsKey("DISABLE_TENCENT_MAVEN_MIRROR")) {
            maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
        } else {
            google()
            jcenter()
        }
        maven {
            name = "GitHubPackages"
            url "https://maven.pkg.github.com/TA-J/shadow_sdk"
            //一个只读账号兼容Github Packages暂时不支持匿名下载
            //https://github.community/t/download-from-github-package-registry-without-authentication/14407
            credentials {
                username = 'TA-J'
                password = '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'
            }
        }
        mavenLocal()
    }
}
apply from: 'buildScripts/gradle/common.gradle'

apply from: "buildScripts/gradle/maven.gradle"